import trim

noncomputable theory
open_locale classical measure_theory nnreal ennreal

variables {α : Type*} {m n : measurable_space α} 

namespace measure_theory

/-- The conditional expectation of an integrable function `f` on a sub-σ-algebra `m` of 
`n` is the Radon-Nikodym derivative between `(μ.with_density_signed_measure f)` restricted 
on `n` and `μ` restricted on `n`. 

Note the ordering where we take the density before the restriction. This is 
because in this case, we will only need integrability on `n` instead `m` which is a 
weaker condition (integrability requies measurabililty almost everywhere). -/
def condexp {m n : measurable_space α} {μ : measure α} (hle : m ≤ n)
  (f : α → ℝ) (hfi : integrable f μ) : α → ℝ := 
signed_measure.radon_nikodym_deriv 
  ((μ.with_density_signed_measure f).trim hle) (μ.trim hle)

namespace condexp

open signed_measure measure

-- possible to change to `sigma_finite`?
variables {μ : measure α} [is_finite_measure μ] {a b : ℝ} {f g : α → ℝ} (hle : m ≤ n) 
  (hfi : integrable f μ) (hgi : integrable g μ)

section

include hle hfi

lemma measurable : measurable[m] (condexp hle f hfi) :=
measurable_radon_nikodym_deriv _ _

lemma integrable : @integrable ℝ _ _ α m (condexp hle f hfi) (μ.trim hle) :=
integrable_radon_nikodym_deriv _ _

lemma condexp_spec {i : set α} (hi : measurable_set[m] i) :
  ∫ x in i, condexp hle f hfi x ∂(μ.trim hle) = ∫ x in i, f x ∂μ :=
begin
  rw [← with_density_signed_measure_trim_eq_integral hle hfi hi,
      ← signed_measure.with_density_radon_nikodym_deriv_eq
        ((μ.with_density_signed_measure f).trim hle) (μ.trim hle) _,
      ← condexp, with_density_signed_measure_apply (integrable _ _) hi],
  { apply_instance },
  { refine vector_measure.absolutely_continuous.mk (λ j hj₁ hj₂, _),
    rw [to_ennreal_vector_measure_apply_measurable hj₁, 
        trim_measurable_set_eq hle hj₁] at hj₂,
    rw [vector_measure.trim_measurable_set_eq hle hj₁, 
        with_density_signed_measure_apply hfi (hle _ hj₁)],
    simp only [restrict_eq_zero.mpr hj₂, integral_zero_measure] }
end

end

/-- If `f` is measurable on a sub-σ-algebra `m`, then the condition of `f` on `n` is 
equal to `f` almost everywhere. -/
lemma ae_eq_of_measurable (hle : m ≤ n) (hfm : measurable[m] f) 
  (hfi : measure_theory.integrable f μ) : 
  condexp hle f hfi =ᵐ[μ.trim hle] f :=
begin
  refine integrable.ae_eq_of_forall_set_integral_eq _ _ (integrable _ _) 
    (hfi.trim hle hfm) (λ i hi _, _),
  rw [condexp_spec hle hfi hi, ← set_integral_trim hle hfm hi],
end

lemma ae_eq_of_measurable' (hle : m ≤ n) (hfm : measurable[m] f) 
  (hfi : measure_theory.integrable f μ) : 
  condexp hle f hfi =ᵐ[μ] f :=
ae_eq_of_ae_eq_trim (ae_eq_of_measurable hle hfm hfi)

-- using `ae_eq_of_ae_eq_trim` we can obtain the ae eq on `μ`

section 

include hle hfi hgi

lemma add : 
  condexp hle (f + g) (hfi.add hgi) =ᵐ[μ.trim hle] 
  condexp hle f hfi + condexp hle g hgi :=
begin
  refine integrable.ae_eq_of_forall_set_integral_eq _ _ (integrable _ _) 
    ((integrable _ _).add (integrable _ _)) (λ i hi _, _),
  rw [condexp_spec hle (hfi.add hgi) hi, 
      integral_add' hfi.integrable_on hgi.integrable_on, 
      ← condexp_spec hle hfi hi, ← condexp_spec hle hgi hi, 
      ← integral_add'],
  { exact @integrable.integrable_on _ _ m _ _ _ _ _ (integrable hle hfi) },
  { exact @integrable.integrable_on _ _ m _ _ _ _ _ (integrable hle hgi) },
end  

lemma add' : 
  condexp hle (f + g) (hfi.add hgi) =ᵐ[μ] 
  condexp hle f hfi + condexp hle g hgi :=
ae_eq_of_ae_eq_trim (add hle hfi hgi)

end

section

include hle hfi

variable {r : ℝ}

lemma smul : 
  condexp hle (r • f) (hfi.smul r) =ᵐ[μ.trim hle] r • condexp hle f hfi :=
begin
  refine integrable.ae_eq_of_forall_set_integral_eq _ _ (integrable _ _) _ _,
  { convert ((integrable hle hfi).smul r) /- exact is slow -/ },
  { intros s hs _,
    erw [condexp_spec hle _ hs, integral_smul, 
         ← condexp_spec hle hfi hs, ← integral_smul],
    refl,
    apply_instance }
end

lemma smul' : 
  condexp hle (r • f) (hfi.smul r) =ᵐ[μ] r • condexp hle f hfi :=
ae_eq_of_ae_eq_trim (smul hle hfi)

end

-- maybe add `neg` and `sub` also

/- Conditioning by a random variable is the same as conditioning on the 
sub-σ-algebra generated by that random variable. -/



end condexp

end measure_theory